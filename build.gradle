plugins {
    id("com.gradleup.shadow") version "8.3.3"
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.0"
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group = 'org.eu.pcraft'
version = '1.1.6'

repositories {
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "https://repo.codemc.io/repository/maven-public/"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    include "de.tr7zw:item-nbt-api:2.14.1"
    implementation("dev.jorel:commandapi-bukkit-shade-mojang-mapped:9.7.0")
    include 'org.bstats:bstats-bukkit:3.0.2'

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

build {
    dependsOn(shadowJar)
}

java{
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

bukkit {
    name = "PepperMinecart"
    main = "org.eu.pcraft.pepperminecart.PepperMinecart"
    apiVersion = "1.16"
    depend = ['Vault']
}

shadowJar {
    configurations = [project.configurations.include]

    relocate "org.bstats", "org.eu.pcraft.PepperMinecart.bstats"
    def dev = Boolean.parseBoolean(project.getProperties().get("dev").toString())
    println "isDev: ${dev}"
    if (!dev) {
        println "Relocation enabled"
        for (var s in new String[]{"org.spongepowered"}) {
            relocate s, "org.eu.pcraft.pepperminecart.${s}"
            println "relocate $s -> org.eu.pcraft.pepperminecart.${s}"
        }
    }
    manifest {
//        attributes["paperweight-mappings-namespace"] = "spigot"
        attributes 'Main-Class': 'org.eu.pcraft.Main'
    }
}

tasks {
    runServer {
        minecraftVersion("1.18")
    }
}
