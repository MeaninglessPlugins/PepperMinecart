plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id "xyz.jpenilla.run-paper" version "2.3.0"
}

group = 'org.eu.pcraft'
version = '1.1.5'

repositories {
    mavenCentral()
    maven {
        url "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        url "https://repo.codemc.io/repository/maven-public/"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    include "de.tr7zw:item-nbt-api:2.12.4"

    compileOnly "dev.jorel:commandapi-bukkit-core:9.3.0"
    include "dev.jorel:commandapi-bukkit-shade:9.3.0"

    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

build {
    dependsOn(shadowJar)
}

java{
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"        //这里最好选择UTF-8
}

shadowJar{
    manifest {
        attributes 'Main-Class': 'org.eu.pcraft.Main'
    }
    configurations = [project.configurations.include]
    relocate("dev.jorel.commandapi", "org.eu.pcraft.packages.commandapi")
    relocate("de.tr7zw.changeme.nbtapi", "org.eu.pcraft.packages.nbtapi")
}

processResources {
    filesMatching("plugin.yml") {
        expand "version": project.version
    }
}

tasks {
    runServer {
        minecraftVersion("1.18")
    }
}
